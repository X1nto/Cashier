plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

android {
    namespace 'com.xinto.cashier'
    compileSdk 33

    defaultConfig {
        applicationId "com.xinto.cashier"
        targetSdk 33
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions += "version"
    productFlavors {
        legacy {
            dimension = "version"
            applicationIdSuffix = ".legacy"
            minSdk = 17
        }

        modern {
            dimension = "version"
            applicationIdSuffix = ".modern"
            minSdk = 21
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose false
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

configurations {
    modernDebugImplementation
    legacyDebugImplementation
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'

    implementation "io.insert-koin:koin-android:3.4.2"

    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    legacyImplementation 'androidx.recyclerview:recyclerview:1.3.1'
    legacyImplementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    legacyImplementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    legacyImplementation 'androidx.fragment:fragment-ktx:1.6.1'

    modernImplementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2'
    modernImplementation 'androidx.activity:activity-compose:1.7.2'
    modernImplementation "androidx.compose.ui:ui:$compose_version"
    modernImplementation "androidx.compose.foundation:foundation:$compose_version"
    modernImplementation "io.insert-koin:koin-androidx-compose:3.4.5"
    modernDebugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    modernDebugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

}